
&НаСервере
Процедура ЗаполнитьИзРегистраНаСервере()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ШаблоныНазванийДокументов.НазваниеДокумента КАК НазваниеДокумента,
	               |	ШаблоныНазванийДокументов.РасширениеНазвания КАК РасширениеНазвания,
	               |	ШаблоныНазванийДокументов.Стадия КАК Стадия,
	               |	ШаблоныНазванийДокументов.ТипДокумента КАК ТипДокумента,
	               |	ШаблоныНазванийДокументов.ПолноеНаименование КАК ПолноеНаименование,
	               |	ШаблоныНазванийДокументов.ШифрНазвания КАК ШифрНазвания
	               |ИЗ
	               |	РегистрСведений.ШаблоныНазванийДокументов КАК ШаблоныНазванийДокументов
	               |ГДЕ
	               |	ШаблоныНазванийДокументов.ШифрНазвания > 0";
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ШифрНазвания = Выборка.ШифрНазвания;
		СпрНазваниеДокументов = Справочники.НазванияДокументов.НайтиПоРеквизиту("ШифрНазвания", ШифрНазвания);
		Если СпрНазваниеДокументов.Пустая() Тогда
			НазваниеДокументаОбъект = Справочники.НазванияДокументов.СоздатьЭлемент();
			ЗаполнитьЗначенияСвойств(НазваниеДокументаОбъект, Выборка);
			НазваниеДокументаОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИзРегистра(Команда)
	
	ЗаполнитьИзРегистраНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВходящиеДокументыНаСервере()
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ДокументВходящий.Ссылка КАК Ссылка,
	               |	ДокументВходящий.НазваниеДокумента КАК НазваниеДокумента,
	               |	ДокументВходящий.ШифрНазвания КАК ШифрНазвания,
	               |	ДокументВходящий.ТипДокумента КАК ТипДокумента,
	               |	ДокументВходящий.Стадия КАК Стадия
	               |ИЗ
	               |	Справочник.ДокументВходящий КАК ДокументВходящий
	               |ГДЕ
	               |	(ДокументВходящий.НазваниеДокумента = &НазваниеДокумента
	               |			ИЛИ ДокументВходящий.ШифрНазвания = &ШифрНазвания)
	               |	И ДокументВходящий.НазваниеДокументаСправочник.Ссылка ЕСТЬ NULL
	               |	И ДокументВходящий.Стадия = &Стадия";
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ВыборкаНазваний = Справочники.НазванияДокументов.Выбрать();
	Пока ВыборкаНазваний.Следующий() Цикл
		Если ВыборкаНазваний.ТипДокумента = Перечисления.ТипДокумента.исх Тогда
			Продолжить;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НазваниеДокумента", ВыборкаНазваний.ПолноеНаименование);
		Запрос.УстановитьПараметр("ШифрНазвания", ВыборкаНазваний.ШифрНазвания);
		Запрос.УстановитьПараметр("Стадия", ВыборкаНазваний.Стадия);
		Выборка = Запрос.Выполнить().Выбрать();
		//"Определение о возврате заявления о выдаче приказа"
		//ОПРЕДЕЛЕНИЕ о возврате заявления о выдаче судебного приказа 
		Пока Выборка.Следующий() Цикл
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.НазваниеДокументаСправочник = ВыборкаНазваний.Ссылка;
			ДокументОбъект.НазваниеДокумента = ВыборкаНазваний.ПолноеНаименование;
			ДокументОбъект.ШифрНазвания = ВыборкаНазваний.ШифрНазвания;
			ДокументОбъект.Записать();
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Записали - " + ДокументОбъект.НазваниеДокумента, ДокументОбъект);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВходящиеДокументы(Команда)
	ОбновитьВходящиеДокументыНаСервере();
КонецПроцедуры
