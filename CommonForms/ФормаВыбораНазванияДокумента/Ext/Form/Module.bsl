
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ШаблоныНазванийДокументов.НазваниеДокумента КАК НазваниеДокумента
	               |ИЗ
	               |	РегистрСведений.ШаблоныНазванийДокументов КАК ШаблоныНазванийДокументов";
	Запрос = Новый Запрос(ТекстЗапроса);
	Выборка = Запрос.Выполнить().Выбрать();
	СписокНазванийДокументов = Элементы.НазваниеДокумента.СписокВыбора;
	СписокНазванийДокументов.Очистить();
	Пока Выборка.Следующий() Цикл
		СписокНазванийДокументов.Добавить(Выборка.НазваниеДокумента);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НазваниеДокументаПриИзмененииНаСервере()
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ШаблоныНазванийДокументов.НазваниеДокумента КАК НазваниеДокумента,
	               |	ШаблоныНазванийДокументов.РасширениеНазвания КАК РасширениеНазвания
	               |ИЗ
	               |	РегистрСведений.ШаблоныНазванийДокументов КАК ШаблоныНазванийДокументов
	               |ГДЕ
	               |	ШаблоныНазванийДокументов.НазваниеДокумента = &НазваниеДокумента";
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НазваниеДокумента", НазваниеДокумента);
	Выборка = Запрос.Выполнить().Выбрать();
	
	СписокРаширенийНазванийДокументов = Элементы.РасширениеНазвания.СписокВыбора;
	СписокРаширенийНазванийДокументов.Очистить();
	Пока Выборка.Следующий() Цикл
		СписокРаширенийНазванийДокументов.Добавить(Выборка.РасширениеНазвания);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура НазваниеДокументаПриИзменении(Элемент)
	
	РасширениеНазвания = "";
	НазваниеДокументаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если ЗначениеЗаполнено(НазваниеДокумента) И ЗначениеЗаполнено(РасширениеНазвания) Тогда
		ПараметрыЗакрытия = Новый Структура;
		НаименованиеДокумента = СтрШаблон("%1 %2", ТРег(НазваниеДокумента), НРег(РасширениеНазвания));
		ПараметрыЗакрытия.Вставить("НаименованиеДокумента", НаименованиеДокумента);
		ЭтаФорма.Закрыть(ПараметрыЗакрытия);
	Иначе
		ПоказатьПредупреждение(, "Не заполнены все поля.");
	КонецЕсли;
	
КонецПроцедуры
