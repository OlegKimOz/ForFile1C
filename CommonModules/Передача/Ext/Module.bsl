Функция ПолучитьОтделДляПередачи(Регион, РольОтдела) Экспорт
	Результат = Справочники.Отделы.ПустаяСсылка();
	
	Если (РольОтдела = Перечисления.РольОтдела.Выездной) ИЛИ (РольОтдела = Перечисления.РольОтдела.Обзвон) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОтделыРегионы.Ссылка КАК Отдел
		               |ИЗ
		               |	Справочник.Отделы.Регионы КАК ОтделыРегионы
		               |ГДЕ
		               |	ОтделыРегионы.Регион = &Регион
		               |	И ОтделыРегионы.Ссылка.РольОтдела = &РольОтдела";
		
		Запрос.УстановитьПараметр("Регион", Регион);
		Запрос.УстановитьПараметр("РольОтдела", РольОтдела);
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	Отделы.Ссылка КАК Отдел
		               |ИЗ
		               |	Справочник.Отделы КАК Отделы
		               |ГДЕ
		               |	Отделы.РольОтдела = &РольОтдела";
		
		Запрос.УстановитьПараметр("РольОтдела", РольОтдела);
	КонецЕсли; 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Отдел;
	КонецЕсли; 
	
	Возврат Результат;
КонецФункции // ПолучитьОтделДляПередачи()

Функция ПередачаДолжника(РольОтдела, Должник, Момент, ОтделДляПередачи) Экспорт
	Перем Передача;
	
	Передача = Документы.ПередачаДолжников.СоздатьДокумент();
	
	Передача.Дата		= Момент;
	Передача.Отдел 		= ОтделДляПередачи;
	Передача.Менеджер 	= Константы.Менеджер.Получить();
	Передача.СотрМенеджер		= Привязка.ПолучитьСотрМенеджера(Должник, Момент);
	Передача.Автор		= ПараметрыСеанса.Пользователь;
	
	НоваяСтрока = Передача.Должники.Добавить();
	НоваяСтрока.Должник = Должник;
		
	Попытка
		Передача.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Сообщить("Не удалось передать должника. " + ОписаниеОшибки(), СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции // ПередачаДолжника()
