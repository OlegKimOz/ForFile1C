
&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДиалогВыбФайла 	 = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбФайла.Заголовок				= "Выберите файл для загрузки:";
	ДиалогВыбФайла.ПолноеИмяФайла			= ПутьКФайлу; //АДРЕС
	ДиалогВыбФайла.Фильтр					= "Excel (*.xlsx)|*.xls*";
	ДиалогВыбФайла.Расширение				= "xlsx";
	ДиалогВыбФайла.МножественныйВыбор		= Ложь;
	ДиалогВыбФайла.ПредварительныйПросмотр	= Ложь;
	ДиалогВыбФайла.Показать(Новый ОписаниеОповещения("ПослеВыбораФайла", ЭтаФорма));
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайла(ВыбранныеФайлы, ДопПарметры) Экспорт
	Если ЗначениеЗаполнено(ВыбранныеФайлы) И ВыбранныеФайлы.Количество() > 0 Тогда
		ПутьКФайлу = ВыбранныеФайлы[0];
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьДанные(Команда)
	Если ЗначениеЗаполнено(ПутьКФайлу) = Ложь Тогда
		ПоказатьПредупреждение(, "Не выбран файл для загрузки. Отмена.")
	Иначе
		ПрочитатьФайл();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Обработан файл " + ПутьКФайлу);
		Элементы.тзДанныеИзФайлаСоздать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКонтакты(Команда)
	СоздатьКонтактыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьКонтактыНаСервере()
	ТЗ_Данные = Новый ТаблицаЗначений;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Телефоны"));
	ТЗ_Данные.Колонки.Добавить("Телефон", Новый ОписаниеТипов(МассивТипов));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Строка"));
	ТЗ_Данные.Колонки.Добавить("Результат", Новый ОписаниеТипов(МассивТипов,,Новый КвалификаторыСтроки(100)));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("СправочникСсылка.Должники"));
	ТЗ_Данные.Колонки.Добавить("Должник", Новый ОписаниеТипов(МассивТипов));
	
	Для Каждого Стр Из Объект.тзДанныеИзФайла Цикл
		СтрНов = ТЗ_Данные.Добавить();
		
		Телефон = Справочники.Телефоны.НайтиПоРеквизиту("Номер", Стр.Телефон);
		СтрНов.Телефон = Телефон;
		СтрНов.Результат = Стр.Результат;
		СтрНов.Должник = Телефон.Владелец;
	КонецЦикла;
	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТЗ_Данные", ТЗ_Данные);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЗ_Данные.Телефон КАК Телефон,
	               |	ТЗ_Данные.Результат КАК Результат,
	               |	ТЗ_Данные.Должник КАК Должник
	               |ПОМЕСТИТЬ ВТ_Данные
	               |ИЗ
	               |	&ТЗ_Данные КАК ТЗ_Данные
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Данные.Телефон КАК Телефон,
	               |	ВТ_Данные.Результат КАК Результат,
	               |	ВТ_Данные.Должник КАК Должник
	               |ИЗ
	               |	ВТ_Данные КАК ВТ_Данные
	               |ИТОГИ ПО
	               |	Должник,
	               |	Результат";
	
	ВыборкаПоДолжникам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Должник");
	
	Пока ВыборкаПоДолжникам.Следующий() Цикл
		
		ВыборкаПоРезультатам = ВыборкаПоДолжникам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Результат");
		
		Пока ВыборкаПоРезультатам.Следующий() Цикл
			
			НовыйКонтакт = Документы.Контакт.СоздатьДокумент();
			НовыйКонтакт.Дата = ТекущаяДатаСеанса();
			НовыйКонтакт.СделатьСтатусОсновным = Истина;
			НовыйКонтакт.Сотрудник = Справочники.Сотрудники.Автоинформатор;
			НовыйКонтакт.Должник = ВыборкаПоДолжникам.Должник;
			Если ВыборкаПоРезультатам.Результат = "отрицательно" Тогда
				НовыйКонтакт.Статус = Справочники.Статусы.НетКонтакта;
			ИначеЕсли ВыборкаПоРезультатам.Результат = "положительно" Тогда
				НовыйКонтакт.Статус = Справочники.Статусы.ОставленаИнформация;
			КонецЕсли;
			
			Выборка = ВыборкаПоРезультатам.Выбрать();
			Пока Выборка.Следующий() Цикл
				НовыйТелефон = НовыйКонтакт.Телефоны.Добавить();
				НовыйТелефон.Телефон = Выборка.Телефон;
			КонецЦикла;
			
			НовыйКонтакт.Записать(РежимЗаписиДокумента.Запись);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создан документ: " + Строка(НовыйКонтакт));
			Попытка
				НовыйКонтакт.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки());
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьФайл()
	ОбъектЭксель = ПолучитьCOMОбъект(ПутьКФайлу);
	ТекущаяСтрока = 1; ПозТелефон = 1; ПозРезультат = 2; Телефон = "";
	
	Телефон = СокрЛП(Строка(ОбъектЭксель.Sheets(1).Cells(ТекущаяСтрока, ПозТелефон).Value));
	Пока ЗначениеЗаполнено(Телефон) Цикл
		ТекущаяСтрока = ТекущаяСтрока + 1;
		Телефон = СокрЛП(Строка(ОбъектЭксель.Sheets(1).Cells(ТекущаяСтрока, ПозТелефон).Value));
		Результат = СокрЛП(Строка(ОбъектЭксель.Sheets(1).Cells(ТекущаяСтрока, ПозРезультат).Value));
			
		НоваяСтрока = Объект.тзДанныеИзФайла.Добавить();
		НоваяСтрока.Телефон = Телефон;
		НоваяСтрока.Результат = Результат;
	КонецЦикла;
КонецПроцедуры